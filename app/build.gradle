import java.util.Properties

apply plugin: 'com.android.application'

def packageName = "com.erandix.movielander"
def appName = "Movie Lander"
def appCode = 1
def appVersion = "1"
def sdkMin = 26       
def sdkCompile = 35
def colorBlack = "#64879A"
def colorPrimary = "#64879A"

def keystorePropsFile = rootProject.file("signing.properties")
def keystoreProps = new Properties()
if (keystorePropsFile.exists()) {
    keystoreProps.load(new FileInputStream(keystorePropsFile))
} else {
    println("⚠️ signing.properties not found. Release builds will fail until you add it. Debug builds are fine.")
}

android {
    namespace packageName
    compileSdk = sdkCompile

    defaultConfig {
        applicationId packageName
        minSdk = sdkMin
        targetSdk = sdkCompile
        versionCode appCode
        versionName appVersion
    }

    signingConfigs {
        release {
            if (keystorePropsFile.exists()) {
                storeFile file(keystoreProps['storeFile'])
                storePassword keystoreProps['storePassword']
                keyAlias keystoreProps['keyAlias']
                keyPassword keystoreProps['keyPassword']
                storeType "pkcs12"
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".apk"
            versionNameSuffix "-Erandix"
        }

        release {
            if (keystorePropsFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    bundle {
        language { enableSplit = false }
        density { enableSplit = false }
        abi { enableSplit = false }
    }

    lint {
        checkReleaseBuilds false
        abortOnError false
    }
}

task updateConfig {
    doLast {
        fileTree(dir: 'src/main/java', include: '**/*.java').each { file ->
            file.text = file.text.replaceAll(/package\s+[^;]+;/, "package ${packageName};")
        }
    }
}

task copyAabToRoot(type: Copy) {
    dependsOn "bundleRelease"
    from("$buildDir/outputs/bundle/release/") { include "*.aab" }
    from("$buildDir/outputs/mapping/release/") { include "mapping.txt" }
    into("$rootDir/output")
    doLast {
        println "✅ AAB and mapping.txt copied to ${rootDir}/output"
    }
}

preBuild.dependsOn updateConfig
build.finalizedBy copyAabToRoot

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}